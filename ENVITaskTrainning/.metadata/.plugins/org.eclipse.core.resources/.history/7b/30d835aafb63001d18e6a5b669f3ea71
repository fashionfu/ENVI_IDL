PRO Publish_ESE_Task, pro_file
  COMPILE_OPT idl2

  ;如果没有输入pro_file，则从剪贴板中获取
  IF ~N_ELEMENTS(pro_file) THEN BEGIN
    pro_file = (CLIPBOARD.get())[0]
    IF ~pro_file.EndsWith('.pro') THEN return
    IF ~FILE_TEST(pro_file) THEN BEGIN
      print, pro_file
      return
    ENDIF
  ENDIF

  print, FILE_BASENAME(pro_file)
  openr,lun,pro_file,/get_lun
  nlines=file_lines(pro_file)
  data=strarr(nlines)
  readf,lun,data
  free_lun,lun
  idx1 = where(data.StartsWith('pro',/fold_case))
  idx2 = where(data.StartsWith('function',/fold_case))
  idx = !NULL
  IF idx1[0] NE -1 THEN idx=idx1
  IF idx2[0] NE -1 THEN idx=[idx,idx2]
  proname = data[idx]
  names=!NULL
  n=N_ELEMENTS(idx)
  FOR i=0,n-1 DO BEGIN
    tmpname = strsplit(proname[i],', ',/extract)
    names = [names, tmpname[1]]
  ENDFOR
  names = '"'+strjoin(names, '","')+'"'

  tmps = strsplit(pro_file, '\', /extract, count=count)

  gsftask_dir = filepath('', root_dir=FILE_DIRNAME(!DIR), subdirectory=['custom_code'])
  envi_custom_code_dir = gsftask_dir+tmps[count-2]

  search_flag = '/*.{pro,task,style}'

  IF ~FILE_TEST(envi_custom_code_dir, /directory) THEN file_mkdir, envi_custom_code_dir

  ;如果有task文件，也一并拷贝过去吧
  taskfiles = FILE_SEARCH(FILE_DIRNAME(pro_file), search_flag, count=taskcount)
  IF taskcount NE 0 THEN BEGIN
    FILE_COPY, taskfiles, envi_custom_code_dir, /allow_same, /overwrite
  ENDIF

  basefilename = FILE_BASENAME(pro_file, '.pro', /fold_case)
  savefile=envi_custom_code_dir+'\'+basefilename+'.sav'

  savecode = "save,"+names+",filename=savefile, /routines"
  r=execute(savecode)
END